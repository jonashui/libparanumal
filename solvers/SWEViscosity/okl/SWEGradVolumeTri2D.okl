/*

The MIT License (MIT)

Copyright (c) 2017-2022 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

@kernel void SWEGradVolumeTri2D(const dlong Nelements,
                                @restrict const  dfloat *  vgeo,
                                @restrict const  dfloat *  cubvgeoCurv,
                                @restrict const  dlong *   mapCurv,
                                @restrict const  dfloat *  Dw,
                                @restrict const  dfloat *  cubPDT,
                                @restrict const  dfloat *  cubPDTs,
                                @restrict const  dfloat *  cubInterp,
                                @restrict const  dfloat *  U,
                                @restrict        dfloat *  gradU){

  for(dlong e=0;e<Nelements;++e;@outer(0)){

    @shared dfloat s_h[p_Np];
    @shared dfloat s_q[p_Np];
    @shared dfloat s_p[p_Np];

    for(int n=0;n<p_Np;++n;@inner(0)){
      const dlong qbase = e*p_Nfields*p_Np + n;
      const dfloat h = U[qbase + 0*p_Np];
      const dfloat q = U[qbase + 1*p_Np];
      const dfloat p = U[qbase + 2*p_Np];

      s_h[n] = h;
      s_q[n] = q;
      s_p[n] = p;
    }


    for(int n=0;n<p_Np;++n;@inner(0)){
      // prefetch geometric factors (constant on triangle)
      const dfloat drdx = vgeo[e*p_Nvgeo + p_RXID];
      const dfloat drdy = vgeo[e*p_Nvgeo + p_RYID];
      const dfloat dsdx = vgeo[e*p_Nvgeo + p_SXID];
      const dfloat dsdy = vgeo[e*p_Nvgeo + p_SYID];

      dfloat dhdr = 0, dhds = 0, dqdr = 0, dqds = 0, dpdr = 0, dpds = 0;

      for(int i=0;i<p_Np;++i){
        const dfloat Drni = Dw[n+i*p_Np+0*p_Np*p_Np];
        const dfloat Dsni = Dw[n+i*p_Np+1*p_Np*p_Np];

        const dfloat h = s_h[i];
        const dfloat q = s_q[i];
        const dfloat p = s_p[i];

        dhdr += Drni*h;
        dhds += Dsni*h;

        dqdr += Drni*q;
        dqds += Dsni*q;

        dpdr += Drni*p;
        dpds += Dsni*p;
      }

      const dfloat dhdx = drdx*dhdr + dsdx*dhds;
      const dfloat dhdy = drdy*dhdr + dsdy*dhds;
      const dfloat dqdx = drdx*dqdr + dsdx*dqds;
      const dfloat dqdy = drdy*dqdr + dsdy*dqds;
      const dfloat dpdx = drdx*dpdr + dsdx*dpds;
      const dfloat dpdy = drdy*dpdr + dsdy*dpds;

      const dlong sbase = e*p_Ngrads*p_Np + n;
      gradU[sbase + 0*p_Np] = -dhdx;
      gradU[sbase + 1*p_Np] = -dhdy;
      gradU[sbase + 2*p_Np] = -dqdx;
      gradU[sbase + 3*p_Np] = -dqdy;
      gradU[sbase + 4*p_Np] = -dpdx;
      gradU[sbase + 5*p_Np] = -dpdy;
    }
  }
}
